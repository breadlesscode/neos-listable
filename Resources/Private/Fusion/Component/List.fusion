prototype(Breadlesscode.Listable:List) < prototype(Neos.Fusion:Component) {
    collection = ${ [] }
    itemsPerPage = ${ 10 }
    itemRenderer = 'Breadlesscode.Listable:ListItem'
    itemName = ${ 'item' }
    paginated = ${ true }
    paginationConfig = Breadlesscode.Listable:PaginationConfig

    @context.currentPage = ${ request.arguments.currentPage || 1 }

    renderer = Neos.Fusion:Array {
        @context.collection = Neos.Fusion:RawArray {
            items = Breadlesscode.Listable:Collection {
                collection = ${ props.collection }
                currentPage = ${ currentPage }
                itemsPerPage= ${ props.itemsPerPage }
                paginate = ${ props.paginated }
            }
            totalCount = ${ Type.getType(props.collection) == 'array' ? q(props.collection).count() : props.collection.count() }
        }
        list = Neos.Fusion:Collection {
            collection = ${ collection.items }
            itemName = ${ props.itemName }
            itemRenderer = Neos.Fusion:Renderer {
                type = ${ props.itemRenderer }
            }
        }
        pagination = Breadlesscode.Listable:Pagination {
            currentPage = ${ currentPage }
            maximumNumberOfLinks = ${ 3 }
            totalCount = ${ collection.totalCount }
            itemsPerPage = ${ props.itemsPerPage }
            paginationConfig = ${ props.paginationConfig }
        }
    }

    @cache {
        mode = 'dynamic'
        entryIdentifier {
            node = ${node}
        }
        entryDiscriminator = ${ request.arguments.currentPage }
        context {
            1 = 'node'
            2 = 'documentNode'
            3 = 'site'
        }
    }
}
