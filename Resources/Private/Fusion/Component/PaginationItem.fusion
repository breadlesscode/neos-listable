prototype(Breadlesscode.Listable:PaginationItem) < prototype(Neos.Fusion:Component) {
    label = ${ false }
    uri = ${ false }
    page = ${ false }
    isCurrent = ${ false }
    typeClass = ${ '' }
    linkClass = 'page-link'

    renderer = Neos.Fusion:Tag {
        tagName = ${ props.uri ? 'a' : 'span' }
        content = ${ props.label }
        attributes {
            href = ${ props.uri }
            href.@if.hasUri = ${ props.uri ? true : false }
            class = ${ props.linkClass }

            aria-label = ${ Translation.translate('Breadlesscode.Listable:Accessibility:item.label') + props.page }
            aria-label.@if.hasUri = ${ props.uri ? true : false }

            aria-current= ${ "true" }
            aria-current.@if.isCurrent = ${ props.isCurrent }
        }

        @process.itemWrap = Neos.Fusion:Tag {
            tagName = 'li'
            content = ${ value }
            attributes {
                class = ${ 'page-item ' + (props.uri ? '' : 'disabled ') + props.typeClass }
            }
        }
    }
}
