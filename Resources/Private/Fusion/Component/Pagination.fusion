prototype(Breadlesscode.Listable:Pagination) < prototype(Neos.Fusion:Component) {
    currentPage = ''
    maximumNumberOfLinks = ''
    totalCount = ''
    itemsPerPage = ''
    paginationConfig = ${ [] }

    @process.wrapPaginationUl = ${ '<ul class="pagination">' + value + '</ul>' }
    @process.wrapPaginationNav = ${ '<nav role="navigation" aria-label="' + Translation.translate('Breadlesscode.Listable:Accessibility:label') + '">' + value + '</nav>' }
    @if.hasOnlyOnePage = ${ this.totalCount > this.itemsPerPage }

    renderer = Neos.Fusion:Collection {
        collection = Breadlesscode.Listable:PaginationArray {
            currentPage = ${ props.currentPage }
            maximumNumberOfLinks = ${ props.maximumNumberOfLinks }
            totalCount = ${ props.totalCount }
            itemsPerPage = ${ props.itemsPerPage }
            paginationConfig = ${ props.paginationConfig }
        }
        itemRenderer = Breadlesscode.Listable:PaginationItem {
            page = ${ item.page }
            isCurrent = ${ String.toInteger(props.currentPage) == String.toInteger(item.page) }
            uri = Neos.Neos:NodeUri {
                node = ${ documentNode }
                additionalParams = Neos.Fusion:DataStructure {
                    currentPage = ${ item.page }
                }
                @if.noSperator = ${ item.page }
            }
            label = ${ item.label }
            typeClass = ${ item.type }
        }
    }
}
